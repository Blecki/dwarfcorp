title: Start
---
<<set_portrait "GUI\diplo-moleman" 64 64 5.0 0 1 0>>
<<show_portrait>>
<<set_language "Molemen">> //Don't set the language - on account of molemen don't speak...? Or do I need a way to do 'speech' without the sound effects?
<<pick>>
	The moleman lifts one claw.
	The moleman grunts.
	Moleman: <incomprehensible chittering>
<<end>>
[[Main]]
===


title: Main
---
<<if $envoy_demands_tribute>>
	<<pick>>
		The moleman holds out his coin purse and points into it. He seems to want payment.
	<<end>>
	[[Pay Tribute|PayTribute]]
	[[Refuse to pay. (WAR)|DeclareWar]]
<<elseif $faction_was_at_war>>
	The moleman brandishes his pick. You remember that you are at war with his people. Is he threatening you?
	[[Make peace.|MakePeace]]
	[[Continue the war.|DeclareWar]]
<<else>>
	[[Trade.|Trade]]
	[[What is your opinion of us?|Opinion]]
	[[What is something you have many of?|Many]]
	[[What is something you have few of?|Few]]
	[[What is something you hate?|Hate]]
	[[What is something you like?|Like]]
	[[Declare war!|ConfirmWar]]
	[[Goodbye.|Goodbye]]
<<endif>>
===


title: Trade
---
<<begin_trade>>
[[Trade2]]
===

title: Trade2
---
<<wait_for_trade>>
<<if $trade_result == "cancelled">>
	<<if $trades == 0>>
		Moleman: <annoyed chittering>
	<<else>>
		Moleman: <chittering>
	<<endif>>
	<<end_trade>>
	[[Main]]
<<elseif $trade_result == "hated">>
	<<pick>>
		The moleman has become angry and begun yelling at his subordinates in their incomprehensible language.
		The moleman throws your offer at your feet. Maybe he didn't like it?
	<<end>>
	<<set $offensive_trades = $offensive_trades + 1>>
	<<political_event "you tried to give us something offensive" -0.25>> // Description, change, duration (days)
	<<if $offensive_trades == 3>>
		The moleman throws his helmet at one of his subordinates. This seems to be the signal for them to leave.
		<<end_trade>>
		<<recall_envoy>>
		<<end_conversation>>
	<<endif>>
	[[Trade2]]
<<elseif $trade_result == "unprofitable">>
	<<pick>>
		The moleman refuses. He shakes his coin purse.
	<<end>>
	[[Trade2]]
<<else>>
	<<set $trades = $trades + 1>>
	<<if $trade_result == "liked">>
		<<political_event "you gave us something we liked" 0.25>>
	<<endif>>
	<<finalize_trade>> // Applys the trade and creates the events the achievement engine needs.
	<<political_event "we had profitable trade" 0.25>>
	<<pick>>
		Moleman: <excited chittering>
		The moleman smiles, showing gold teeth.
	<<end>>
	[[Trade2]]
<<endif>>
<<end_trade>>
[[Main]]
===


title: Opinion
---
The moleman shrugs. Your advisor reminds you of recent events, and you surmise that your current relationship with the molemen is $envoy_relationship.
[[Main]]
===


title: Many
---
The moleman points at the ground. They live underground - dwarves have a lot in common with them, including an abundance of soil, stone, and gems.
[[Main]]
===


title: Few
---
The moleman points at items in your stockpile. Wood. Magic. Metal. Those are all things he wants, apparently.
[[Main]]
===


title: Hate
---
The moleman squints, shielding his eyes with one hand. Just being on the surface is difficult for them - anything related to the surface is distasteful for them.
[[Main]]
===


title: Like
---
The moleman removes his helmet and knocks on it. Yes, metal, of course. 
[[Main]]
===


title: ConfirmWar
---
Moleman: <chittering>
[[Yes!|DeclareWar]]
[[No.|Main]]
===


title: DeclareWar
---
The moleman extends a claw toward you, then motions as if he is slitting his own throat.
<<political_event "you declared war on us" -2.0>>
<<declare_war>> // This is what actually turns them hostile. Also generates the events for the achievement system.
<<recall_envoy>>
<<end_conversation>>
===


title: MakePeace
---
The moleman shrugs.
<<make_peace>>
<<political_event "you made peace with us" 0.4>>
[[Main]]
===


title: PayTribute
---
<<if $player_money < $envoy_tribute_demanded>>
	Moleman: <annoyed chittering>
	<<recall_envoy>>
	<<end_conversation>>
<<else>>
	<<pay_tribute>>
	<<political_event "you paid us tribute" 0.5>>
	The moleman smiles, showing gold teeth.
	<<set $envoy_tribute_demanded = 0.0>>
	[[Main]]
<<endif>>
===


title: Goodbye
---
The moleman bows and leaves.
<<end_conversation>>
===  