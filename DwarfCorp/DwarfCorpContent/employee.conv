title: Start
---
<<set_language "Dwarf">>
<<if $is_asleep>>
[[Sleep]]
<<endif>>
<<if $is_on_strike>>
[[Strike]]
<<endif>>
<<if $personality == "grumpy">>
   <<pick>>
   $employee_name: What do you want?
   $employee_name: Yeah?
   $employee_name: Yeah boss?
   $employee_name: Uh, hi.
   $employee_name: Yo.
   <<end>>
<<elseif $personality == "happy">>
    <<pick>>
	$employee_name: Hello!
	$employee_name: $motto!
	$employee_name: Good $time_of_day!
	$employee_name: How are you?
	$employee_name: How can I help you?
	<<end>>
<<elseif $personality == "anxious">>
    <<pick>>
	$employee_name: Did I do something wrong?
	$employee_name: Oh... hi boss.
	$employee_name: W...what's up?
	$employee_name: Oh! Did you want me?
	<<end>>
<<endif>>
[[Main]]
===

title: Main
---
[[How are you?|HowAreYou]]
[[How's your health?|Health]]
[[What are you doing?|WhatAreYouDoing]]
[[Goodbye|Bye]]
===

title: HowAreYou
---
<<if $employee_status == "OK">>
	<<if $personality == "grumpy">>
	   <<pick>>
	   $employee_name: Things could be worse.
	   $employee_name: I'm fine.
	   $employee_name: $company_name isn't the worst.
	   $employee_name: I'm satisfied I guess.
	   $employee_name: Things are acceptable.
	   <<end>>
	<<elseif $personality == "happy">>
		<<pick>>
		$employee_name: $company_name is the best!
		$employee_name: I'm doing just fine.
		$employee_name: I'm doing great!
		$employee_name: Things are peachy.
		<<end>>
	<<elseif $personality == "anxious">>
		<<pick>>
		$employee_name: I'm OK.
		$employee_name: I'm doing fine. Why, is something wrong?
		$employee_name: I'm having a good day.
		$employee_name: I'm satisfied.
		<<end>>
	<<endif>>
<<else>>
	<<if $personality == "grumpy">>
	   <<pick>>
	   $employee_name: I'm $employee_status. What does it look like?
	   $employee_name: $employee_status, obviously.
	   $employee_name: Aside from being completely $employee_status, I'm great boss!
	   $employee_name: Your employees are $employee_status, boss.
	   $employee_name: $employee_status.
	   <<end>>
	<<elseif $personality == "happy">>
		<<pick>>
		$employee_name: I'm a bit $employee_status actually.
		$employee_name: Thanks for asking, but I'm $employee_status.
		$employee_name: Feeling a bit $employee_status lately.
		$employee_name: Actually, I'm a little $employee_status.
		<<end>>
	<<elseif $personality == "anxious">>
		<<pick>>
		$employee_name: Don't panic, but I'm $employee_status.
		$employee_name: I'm $employee_status! Please don't fire me!
		$employee_name: I'm $employee_status, why?
		$employee_name: I'm ... okay I'm $employee_status.
		<<end>>
	<<endif>>
<<endif>>
<<if $employee_status == "Unhappy">>
	[[What's wrong?|WhyUnhappy]]
    [[OK|Main]]
<<else>>
    [[What else?|Thoughts]]
	[[OK|Main]]
<<endif>>
===

title: Health
---
$employee_name: I have $injuries.
[[OK|Main]]
===

title: Bye
---
<<if $personality == "grumpy">>
   <<pick>>
   $employee_name: K.
   $employee_name: I gotta go.
   $employee_name: Bye.
   $employee_name: I'm off.
   $employee_name: Back to work.
   <<end>>
<<elseif $personality == "happy">>
    <<pick>>
	$employee_name: As they say, $motto!
	$employee_name: Bye!
	$employee_name: Talk to you later boss!
	$employee_name: See you later!
	<<end>>
<<elseif $personality == "anxious">>
    <<pick>>
	$employee_name: Um... $motto?
	$employee_name: Whew ok, that's it?
	$employee_name: Alright, bye.
	$employee_name: So much work to do!
	<<end>>
<<endif>>
<<end_conversation>>
===

title: Sleep
---
$employee_name: zzzzzz...
<<end_conversation>>
===

title: Strike
---
<<if $personality == "grumpy">>
   <<pick>>
   $employee_name: I am NOT happy to see you right now!
   $employee_name: I can't believe you're talking to ME.
   $employee_name: Ugh, you again.
   $employee_name: Check it out, the world's worst boss.
   $employee_name: Nice colony. NOT!
   $employee_name: Just another bad day at $company_name.
   <<end>>
<<elseif $personality == "happy">>
    <<pick>>
	$employee_name: FYI, I'm on strike right now.
	$employee_name: I'm taking a break. I hope you understand.
	$employee_name: I just need some me time right now.
	$employee_name: I won't lie, I have some problems.
	<<end>>
<<elseif $personality == "anxious">>
    <<pick>>
	$employee_name: I can't do this anymore!
	$employee_name: Everything's out of control!
	$employee_name: Ahh!!!
	$employee_name: I'm having a nervous breakdown!
	<<end>>
<<endif>>
[[What's wrong?|WhyUnhappy]]
[[What do you want?|DemandPayment]]
===

title: WhyUnhappy
---
$employee_name: $grievences

<<if $is_on_strike>>
    [[OK|Strike]]
<<else>>
	[[OK|Main]]
<<endif>>
===

title: Thoughts
---
<<if $grievences == "">>
	<<if $good_things == "">>
		$employee_name: Nothing really.
	<<else>>
		$employee_name: $good_things
	<<endif>>
<<else>>
   <<if $good_things == "">>
       $employee_name: $grievences.
   <<else>>
       $employee_name: $good_things
	   $employee_name: But $grievences
   <<endif>>
<<endif>>
[[OK|Main]]
===

title: DemandPayment
---
$employee_name: If you give me $employee_bonus DwarfBux, I'll get back to work.
<<if $company_money >= $employee_bonus>>
	[[OK (pay them a bonus)|PayBonus]]
	[[No|Strike]]
<<else>>
	[[We don't have enough.|Strike]]
<<endif>>
===

title: PayBonus
---
<<pay_employee_bonus>>
<<set $is_on_strike = 0>>
<<set $company_money = $company_money - $employee_bonus>>
<<end_strike>>
<<add_thought "I got a bonus" 30 16>>
$employee_name: Thanks! I think that will do!
[[OK|Main]]
===

title: WhatAreYouDoing
---
$employee_name: I'm a $employee_class. My assignment is to: $employee_task.
[[OK|Main]]
===